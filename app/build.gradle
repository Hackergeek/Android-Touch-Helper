apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

// http://www.race604.com/android-auto-version/
// use git count as build number, as current date as build version
def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy.MM.dd')
    return formattedDate
}
def cmd = 'git rev-list HEAD --count'
def gitVersion = cmd.execute().text.trim().toInteger() + 100
def appVersion = getDate()


android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.zfdang.touchhelper"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode gitVersion
        versionName appVersion

        // Enabling multidex support.
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("touch_helper.keystore")
            storePassword "touchhelper"
            keyAlias "touchhelper"
            keyPassword "touchhelper"
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    // http://www.sollyu.com/android-apk-studio-generated-automatically-appends-a-version-number/
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if(buildType.name == "release") {
                output.outputFileName = new File("TouchHelper" + "-" + buildType.name + "-v" + defaultConfig.versionName + ".apk" );
            } else {
                output.outputFileName = new File("TouchHelper" + "-" + buildType.name + "-v" + defaultConfig.versionName + ".apk" );
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'com.google.code.gson:gson:2.8.6'

    // https://developer.android.com/jetpack/androidx/releases/preference?hl=zh-cn
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // https://mvnrepository.com/artifact/com.belerweb/pinyin4j
    implementation group: 'com.belerweb', name: 'pinyin4j', version: '2.5.1'
    implementation "androidx.core:core-ktx:1.5.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}